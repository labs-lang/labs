system {
    extern = _birds, _grid, _delta
    spawn = Bird: _birds   
}

stigmergy Cohesion {
    link =
        ((x of c1 - x of c2) * (x of c1 - x of c2)) + 
        ((y of c1 - y of c2) * (y of c1 - y of c2)) > _delta * _delta
        and count of c1 >= count of c2
        and leader of c1 != leader of c2

    posx, posy: -1, -1
}

stigmergy Repulsion {
    link = abs(x of c1 - x of c2) < 2 and abs(y of c1 - y of c2) < 2

    repX, repY: -1, -1
}

stigmergy Alignment {
    link =
        ((x of c1 - x of c2) * (x of c1 - x of c2)) + 
        ((y of c1 - y of c2) * (y of c1 - y of c2)) <= _delta * _delta

    dirx, diry, leader, count: [-1, 1], [-1, 1], id, 0
}

agent Bird {
    interface = x: 0.._grid; y: 0.._grid
    stigmergies = Cohesion; Alignment; Repulsion
    
    # At first, every agent is the leader of their own swarm.
    # Exchange of stigmergic messages naturally leads to the
    # election of a leader for each group of connected birds
    Behavior = leader <~ id; Loop
    Loop = Move; Attract; Loop


    # Only move if there is no agent on the destination position
    Move =  
        x, y <- (x + dirx) % _grid, (y + diry) % _grid

    Attract = (
            # Leaders only signal their own position
            (leader = id -> posx, posy <~ x, y)
            ++
            # Followers upgrade the count of their swarm
            (leader != id -> count <~ count + 1)
        );
        
            # When a follower gets (posx, posy) != (-1, -1) it means that it
            # has sensed a swarm "better" than the current one. Thus, is 
            # changes its direction to point towards its new leader.
            # Notice that this will trigger feedback loops among other agents 
            # near it.
        (x != posx and posx != -1 -> dirx <~ (posx-x) / abs(posx-x)); 
        (y != posy and posy != -1 -> diry <~ (posy-y) / abs(posy-y))
        
}

check {
    # Count = finally exists Bird b, count of b = _birds 
    OneLeader = finally forall Bird b1, forall Bird b2, 
        leader of b1 = leader of b2
        #dirx of b1 = dirx of b2
        #and
        #diry of b1 = diry of b2
}